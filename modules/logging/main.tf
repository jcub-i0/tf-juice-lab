
resource "aws_cloudtrail" "cloudtrail" {
  depends_on = [
    aws_s3_bucket_policy.centralized_logs_policy,
    var.cloudtrail_to_cw_role,
    var.cloudtrail_to_cw_policy,
    var.cloudtrail_logs
  ]

  name                          = "CloudTrail"
  s3_bucket_name                = aws_s3_bucket.centralized_logs.bucket
  include_global_service_events = true
  is_multi_region_trail         = true
  enable_log_file_validation    = true

  kms_key_id     = var.kms_key_arn
  sns_topic_name = var.cloudtrail_notifications_name

  cloud_watch_logs_group_arn = "${var.cloudtrail_logs_arn}:*"
  cloud_watch_logs_role_arn  = var.cloudtrail_to_cw_role_arn

  insight_selector {
    insight_type = "ApiCallRateInsight"
  }

  insight_selector {
    insight_type = "ApiErrorRateInsight"
  }
}

# Centralized Logging S3 Bucket resources

resource "aws_s3_bucket" "centralized_logs" {
  bucket = "juice-shop-logs-${var.random_suffix_hex}"

  tags = {
    Name        = "TF-Juice-Lab Logs"
    Environment = var.environment
    Purpose     = "Centralized location to store logs generated by services running in the environment"
  }
}

resource "aws_s3_bucket_lifecycle_configuration" "centralized_logs_lifecycle" {
  depends_on = [aws_s3_bucket_versioning.logs_bucket]

  bucket = aws_s3_bucket.centralized_logs.id

  rule {
    id     = "log-expiration"
    status = "Enabled"

    expiration {
      days = 90
    }
    abort_incomplete_multipart_upload {
      days_after_initiation = 7
    }

    filter {
      prefix = ""
    }
  }
}

## Enable SSE encryption on centralized_logs bucket
resource "aws_s3_bucket_server_side_encryption_configuration" "logs_bucket_encrypt" {
  bucket = aws_s3_bucket.centralized_logs.bucket

  rule {
    apply_server_side_encryption_by_default {
      kms_master_key_id = var.kms_master_key_arn
      sse_algorithm     = "aws:kms"
    }
  }
}

resource "aws_s3_bucket_versioning" "logs_bucket" {
  bucket = aws_s3_bucket.centralized_logs.bucket
  versioning_configuration {
    status = "Enabled"
  }
}

resource "aws_s3_bucket_public_access_block" "centralized_logs_public_block" {
  bucket = aws_s3_bucket.centralized_logs.id

  block_public_acls       = true
  block_public_policy     = true
  restrict_public_buckets = true
  ignore_public_acls      = true
}

resource "aws_s3_bucket_replication_configuration" "centralized_logs_replication" {
  bucket = aws_s3_bucket.centralized_logs.bucket
  role   = var.replication_role_arn

  depends_on = [var.centralized_logs_replica_bucket]

  rule {
    id     = "centralized-logs-crr"
    status = "Enabled"

    destination {
      bucket        = var.centralized_logs_replica_bucket_s3_bucket_arn
      storage_class = "STANDARD_IA"
      encryption_configuration {
        replica_kms_key_id = var.kms_replica_secondary_region_key_arn
      }
    }

    filter {
      prefix = ""
    }

    source_selection_criteria {
      replica_modifications {
        status = "Enabled"
      }

      sse_kms_encrypted_objects {
        status = "Enabled"
      }
    }

    delete_marker_replication {
      status = "Enabled"
    }
  }
}

resource "aws_s3_bucket_notification" "centralized_logs" {
  bucket = aws_s3_bucket.centralized_logs.bucket

  topic {
    topic_arn     = aws_sns_topic.centralized_logs_bucket_notifications.arn
    events        = ["s3:ObjectCreated:*"]
    filter_prefix = "logs/"
  }
  depends_on = [
    aws_s3_bucket.centralized_logs,
    aws_sns_topic.centralized_logs_bucket_notifications,
    var.centralized_logs_topic_policy
  ]
}

resource "aws_sqs_queue" "centralized_logs_s3_event_queue" {
  name              = "centralized-logs-s3-notifications"
  kms_master_key_id = var.kms_key_arn
}

resource "aws_sns_topic" "centralized_logs_bucket_notifications" {
  name              = "centralized-logs-s3-notifications"
  kms_master_key_id = var.kms_key_arn
}

resource "aws_sns_topic_subscription" "centralized_logs_bucket_notifications_sub" {
  topic_arn = aws_sns_topic.centralized_logs_bucket_notifications.arn
  protocol  = "sqs"
  endpoint  = aws_sqs_queue.centralized_logs_s3_event_queue.arn
}

resource "aws_config_delivery_channel" "config_delivery_channel" {
  name           = "Config-Delivery-Channel"
  s3_bucket_name = aws_s3_bucket.centralized_logs.bucket
  depends_on     = [var.config_configuration_recorder_config_rec]
}

## S3 Bucket policy to allow CloudTrail to put objects in Centralized Logs bucket
resource "aws_s3_bucket_policy" "centralized_logs_policy" {
  bucket = aws_s3_bucket.centralized_logs.bucket

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid    = "AWSCloudTrailWrite"
        Effect = "Allow"
        Principal = {
          Service = "cloudtrail.amazonaws.com"
        }
        Action   = "s3:PutObject"
        Resource = "${aws_s3_bucket.centralized_logs.arn}/AWSLogs/${data.aws_caller_identity.current.account_id}/*"
        Condition = {
          StringEquals = {
            "s3:x-amz-acl" = "bucket-owner-full-control"
          }
        }
      },
      {
        Sid    = "AWSCloudTrailListBucket"
        Effect = "Allow"
        Principal = {
          Service = "cloudtrail.amazonaws.com"
        }
        Action   = "s3:ListBucket"
        Resource = aws_s3_bucket.centralized_logs.arn
        Condition = {
          StringEquals = {
            "s3:prefix" = "AWSLogs/${data.aws_caller_identity.current.account_id}/"
          }
        }
      },
      {
        Sid    = "AWSCloudTrailGetBucketAcl"
        Effect = "Allow"
        Principal = {
          Service = "cloudtrail.amazonaws.com"
        }
        Action   = "s3:GetBucketAcl"
        Resource = aws_s3_bucket.centralized_logs.arn
      },
      {
        Sid    = "AllowS3AccessLogging"
        Effect = "Allow"
        Principal = {
          Service = "logging.s3.amazonaws.com"
        }
        Action   = "s3:PutObject"
        Resource = "${aws_s3_bucket.centralized_logs.arn}/s3-access-logs/*"
        Condition = {
          StringEquals = {
            "aws:SourceAccount" = data.aws_caller_identity.current.account_id
          }
        }
      },
      {
        Sid    = "AllowReplicationRoleReadFromLogsSource"
        Effect = "Allow"
        Principal = {
          AWS = var.replication_role_arn
        }
        Action = [
          "s3:GetObjectVersion",
          "s3:GetObjectVersionAcl",
          "s3:GetObjectVersionForReplication",
          "s3:GetObjectLegalHold",
          "s3:GetObjectRetention",
          "s3:GetObjectTagging",
          "s3:ListBucket",
          "s3:ListBucketVersions"
        ]
        Resource = [
          aws_s3_bucket.centralized_logs.arn,
          "${aws_s3_bucket.centralized_logs.arn}/*"
        ]
      }
    ]
  })
}

### Attach IAM policy that allows General Purpose S3 to publish to Centralized Logs SNS topic
resource "aws_sns_topic_policy" "general_purpose_topic_policy" {
  arn    = aws_sns_topic.general_purpose_bucket_notifications.arn
  policy = module.iam.general_purpose_sns_policy_json
}

### Attach IAM policy that allows Centralized Logs S3 Notifications SNS to send messages to SQS
resource "aws_sqs_queue_policy" "centralized_logs_s3_sns_to_sqs" {
  queue_url = module.logging.centralized_logs_s3_event_queue_id
  policy    = module.iam.centralized_logs_s3_sns_to_sqs_json
}

### Attach IAM policy that allows Centralized Logs S3 to publish to Centralized Logs SNS topic
resource "aws_sns_topic_policy" "centralized_logs_topic_policy" {
  arn    = module.logging.centralized_logs_bucket_notifications_arn
  policy = module.iam.centralized_logs_topic_policy_json
}